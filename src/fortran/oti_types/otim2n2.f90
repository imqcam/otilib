!! ================================================================
!!
!! A FORTRAN MODULE FOR  USE OF OTI NUMBERS.
!! IN PARTICULAR, OTI ALGEBRA WITH UP TO M = 2 IMAGINARY BASES AND 
!! TRUNCATION ORDER N = 2
!!
!! @AUTHOR
!! MAURICIO ARISTIZABAL               \N 
!! UNIVERSIDAD EAFIT                  \N 
!!

!> MODULE TO MANIPULATE OTI NUMBERS AND VECTOR/MATRICES OF OTI NUMBERS.
MODULE OTIM2N2
  IMPLICIT NONE

  
  INTEGER, PARAMETER :: REAL_KIND  = 8
  INTEGER, PARAMETER :: NUM_IM_DIR = 5
  INTEGER, PARAMETER :: TORDER     = 2
  
  ! OTI SCALAR TYPE. 
  TYPE ONUM_M2N2
     REAL(REAL_KIND) :: R
     ! ORDER 1
     REAL(REAL_KIND) :: E1  ! \EPSILON_1
     REAL(REAL_KIND) :: E2  ! \EPSILON_2
     ! ORDER 2
     REAL(REAL_KIND) :: E11 ! \EPSILON_1^2
     REAL(REAL_KIND) :: E12 ! \EPSILON_1\EPSILON_2
     REAL(REAL_KIND) :: E22 ! \EPSILON_2^2
  END TYPE ONUM_M2N2


  ! OTI VECTOR TYPE. 
  TYPE OVEC_M2N2
     REAL(REAL_KIND), DIMENSION(:), ALLOCATABLE :: R
     ! ORDER 1
     REAL(REAL_KIND), DIMENSION(:), ALLOCATABLE :: E1  ! \EPSILON_1
     REAL(REAL_KIND), DIMENSION(:), ALLOCATABLE :: E2  ! \EPSILON_2
     ! ORDER 2
     REAL(REAL_KIND), DIMENSION(:), ALLOCATABLE :: E11 ! \EPSILON_1^2
     REAL(REAL_KIND), DIMENSION(:), ALLOCATABLE :: E12 ! \EPSILON_1\EPSILON_2
     REAL(REAL_KIND), DIMENSION(:), ALLOCATABLE :: E22 ! \EPSILON_2^2
  END TYPE OVEC_M2N2

    ! OTI VECTOR TYPE. 
  TYPE OMAT_M2N2
     REAL(REAL_KIND), DIMENSION(:,:), ALLOCATABLE :: R
     ! ORDER 1
     REAL(REAL_KIND), DIMENSION(:,:), ALLOCATABLE :: E1  ! \EPSILON_1
     REAL(REAL_KIND), DIMENSION(:,:), ALLOCATABLE :: E2  ! \EPSILON_2
     ! ORDER 2
     REAL(REAL_KIND), DIMENSION(:,:), ALLOCATABLE :: E11 ! \EPSILON_1^2
     REAL(REAL_KIND), DIMENSION(:,:), ALLOCATABLE :: E12 ! \EPSILON_1\EPSILON_2
     REAL(REAL_KIND), DIMENSION(:,:), ALLOCATABLE :: E22 ! \EPSILON_2^2
  END TYPE OMAT_M2N2
! =============================================================================
! ============================== OVERLOADED OPERATORS =========================
! =============================================================================

! INTIALIZATION OPERATIONS
INTERFACE ASSIGNMENT (=)
  MODULE PROCEDURE  ONUMM2N2_ASSIGN_REAL
END INTERFACE

! PRETTY PRINTING FOR TYPES
INTERFACE PPRINT
  MODULE PROCEDURE ONUMM2N2_PRINT
END INTERFACE


! SCALAR OPERATORS
! ADDITION
INTERFACE OPERATOR(+)
   MODULE PROCEDURE ONUMM2N2_ADD_ONUMM2N2, REAL_ADD_ONUMM2N2,ONUMM2N2_ADD_REAL
END INTERFACE

! SUBTRACTION
INTERFACE OPERATOR(-)
   MODULE PROCEDURE ONUMM2N2_SUB_ONUMM2N2
END INTERFACE

! MULTIPLICATION
INTERFACE OPERATOR(*)
   MODULE PROCEDURE ONUMM2N2_MUL_ONUMM2N2, REAL_MUL_ONUMM2N2, ONUMM2N2_MUL_REAL
END INTERFACE

! DIVISION
INTERFACE OPERATOR(/)
   MODULE PROCEDURE ONUMM2N2_DIV_REAL, ONUMM2N2_DIV_ONUMM2N2
END INTERFACE

! OCTONION POWER OPERATOR
INTERFACE operator(**)
   MODULE PROCEDURE ONUMM2N2_IPOW, ONUMM2N2_POW
END INTERFACE

! TRASCENDENTAL FUNCTIONS
! SINE
INTERFACE SIN
  MODULE PROCEDURE ONUMM2N2_SIN
END INTERFACE

! COSINE
INTERFACE COS
  MODULE PROCEDURE ONUMM2N2_COS
END INTERFACE

CONTAINS
  
  ! =============================================================================
  ! ===========================FUNCTIONS ============================
  ! =============================================================================
  

  ! ADDITION OF TWO OTI NUMBERS
  SUBROUTINE ONUMM2N2_ASSIGN_REAL(RES, RHS)
    
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(OUT) :: RES
    REAL(REAL_KIND), INTENT(IN)  :: RHS

    ! SET REAL PART AND ALL OTHER IMAGINARY DIRECTIONS AS 0.
    RES%R    = RHS 
    RES%E1   = 0.0D0
    RES%E2   = 0.0D0
    RES%E11  = 0.0D0
    RES%E12  = 0.0D0
    RES%E22  = 0.0D0

  END SUBROUTINE ONUMM2N2_ASSIGN_REAL


  ! Pretty print fucntion.
  SUBROUTINE ONUMM2N2_PRINT(ONUM,fmt)
    IMPLICIT NONE
    TYPE(ONUM_M2N2),  INTENT(IN) :: ONUM
    CHARACTER(len=*), INTENT(IN), OPTIONAL :: fmt
    CHARACTER(len=:), ALLOCATABLE :: output_format


    IF (PRESENT(fmt)) THEN
      output_format = '('//trim(fmt)//','// &
                           trim(fmt)//','// &
                           trim(fmt)//','// &
                           trim(fmt)//','// &
                           trim(fmt)//','// &
                           trim(fmt)//')'
    ELSE
      output_format = '(ES11.3,ES11.3,ES11.3,ES11.3,ES11.3,ES11.3)'
    END IF 

    ! PRINT CALL.
    write(*,output_format)  &
      ONUM%R  ,             &
      ONUM%E1 ,             &
      ONUM%E2 ,             &
      ONUM%E11,             &
      ONUM%E12,             &
      ONUM%E22

  END SUBROUTINE






  ! ===============   SCALAR OPERATIONS  ==================
  ! ADDITION OF TWO OTI NUMBERS
  ELEMENTAL FUNCTION REAL_ADD_ONUMM2N2(LHS, RHS) &
    RESULT(RES)
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(IN) :: RHS 
    REAL(REAL_KIND), INTENT(IN) :: LHS 
    TYPE(ONUM_M2N2) :: RES

    ! ADD ALL IMAGINARY DIRECTIONS.

    RES%R    = LHS + RHS%R  

    RES%E1   = RHS%E1 
    RES%E2   = RHS%E2 

    RES%E11  = RHS%E11
    RES%E12  = RHS%E12
    RES%E22  = RHS%E22

  END FUNCTION REAL_ADD_ONUMM2N2

  ELEMENTAL FUNCTION ONUMM2N2_ADD_REAL(LHS, RHS) &
    RESULT(RES)
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(IN) :: LHS 
    REAL(REAL_KIND), INTENT(IN) :: RHS 
    TYPE(ONUM_M2N2) :: RES

    ! ADD ALL IMAGINARY DIRECTIONS.
    RES%R    = LHS%R   + RHS  

    RES%E1   = LHS%E1 
    RES%E2   = LHS%E2 

    RES%E11  = LHS%E11
    RES%E12  = LHS%E12
    RES%E22  = LHS%E22

  END FUNCTION ONUMM2N2_ADD_REAL

  ELEMENTAL FUNCTION ONUMM2N2_ADD_ONUMM2N2(LHS, RHS) &
    RESULT(RES)
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(IN) :: LHS, RHS
    TYPE(ONUM_M2N2) :: RES

    ! ADD ALL IMAGINARY DIRECTIONS.
    RES%R    = LHS%R   + RHS%R  

    RES%E1   = LHS%E1  + RHS%E1 
    RES%E2   = LHS%E2  + RHS%E2 

    RES%E11  = LHS%E11 + RHS%E11
    RES%E12  = LHS%E12 + RHS%E12
    RES%E22  = LHS%E22 + RHS%E22

  END FUNCTION ONUMM2N2_ADD_ONUMM2N2

  ! SUBTRACTION OF TWO OTI NUMBERS
  ELEMENTAL FUNCTION ONUMM2N2_SUB_REAL(LHS, RHS) &
    RESULT(RES)
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(IN) :: LHS
    REAL(REAL_KIND), INTENT(IN) :: RHS
    TYPE(ONUM_M2N2) :: RES

    RES   = LHS
    RES%R = LHS%R   - RHS

  END FUNCTION ONUMM2N2_SUB_REAL
  
  ! SUBTRACTION OF TWO OTI NUMBERS
  ELEMENTAL FUNCTION REAL_SUB_ONUMM2N2(LHS, RHS) &
    RESULT(RES)
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(IN) :: RHS
    REAL(REAL_KIND), INTENT(IN) :: LHS
    TYPE(ONUM_M2N2) :: RES

    ! SUBTRACT ALL IMAGINARY DIRECTIONS.

    RES%R    = LHS - RHS%R  

    RES%E1   = - RHS%E1 
    RES%E2   = - RHS%E2 

    RES%E11  = - RHS%E11
    RES%E12  = - RHS%E12
    RES%E22  = - RHS%E22

  END FUNCTION REAL_SUB_ONUMM2N2

  ! SUBTRACTION OF TWO OTI NUMBERS
  ELEMENTAL FUNCTION ONUMM2N2_SUB_ONUMM2N2(LHS, RHS) &
    RESULT(RES)
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(IN) :: LHS, RHS
    TYPE(ONUM_M2N2) :: RES

    ! SUBTRACT ALL IMAGINARY DIRECTIONS.

    RES%R    = LHS%R   - RHS%R  

    RES%E1   = LHS%E1  - RHS%E1 
    RES%E2   = LHS%E2  - RHS%E2 

    RES%E11  = LHS%E11 - RHS%E11
    RES%E12  = LHS%E12 - RHS%E12
    RES%E22  = LHS%E22 - RHS%E22

  END FUNCTION ONUMM2N2_SUB_ONUMM2N2


  ! MULTIPLICATION OF TWO OTI NUMBERS
  ELEMENTAL FUNCTION ONUMM2N2_MUL_ONUMM2N2(LHS, RHS) &
    RESULT(RES)
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(IN) :: LHS, RHS
    TYPE(ONUM_M2N2) :: RES

    ! MULTIPLY ALL IMAGINARY DIRECTIONS.

    RES%R    = LHS%R*RHS%R  

    RES%E1   = LHS%E1*RHS%R + LHS%R*RHS%E1
    RES%E2   = LHS%E2*RHS%R + LHS%R*RHS%E2

    RES%E11  = LHS%E1*RHS%E1 + LHS%E11*RHS%R + LHS%R*RHS%E11
    RES%E12  = LHS%E1*RHS%E2 + LHS%E2*RHS%E1 + LHS%E12*RHS%R + LHS%R*RHS%E12
    RES%E22  = LHS%E2*RHS%E2 + LHS%E22*RHS%R + LHS%R*RHS%E22

  END FUNCTION ONUMM2N2_MUL_ONUMM2N2

  ! MULTIPLICATION OF OTI NUMBER TIMES REAL NUMBER
  ELEMENTAL FUNCTION ONUMM2N2_MUL_REAL(LHS, RHS) &
    RESULT(RES)
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(IN) :: LHS
    REAL(REAL_KIND), INTENT(IN) :: RHS
    TYPE(ONUM_M2N2) :: RES

    ! MULTIPLY ALL IMAGINARY DIRECTIONS.

    RES%R    = LHS%R*RHS

    RES%E1   = LHS%E1*RHS
    RES%E2   = LHS%E2*RHS

    RES%E11  = LHS%E11*RHS
    RES%E12  = LHS%E12*RHS
    RES%E22  = LHS%E22*RHS

  END FUNCTION ONUMM2N2_MUL_REAL

  ! MULTIPLICATION OF OTI NUMBER TIMES REAL NUMBER
  ELEMENTAL FUNCTION REAL_MUL_ONUMM2N2(LHS, RHS) &
    RESULT(RES)
    IMPLICIT NONE
    
    REAL(REAL_KIND), INTENT(IN) :: LHS
    TYPE(ONUM_M2N2), INTENT(IN) :: RHS
    TYPE(ONUM_M2N2) :: RES

    ! MULTIPLY ALL IMAGINARY DIRECTIONS.

    RES%R    = LHS * RHS%R

    RES%E1   = LHS * RHS%E1
    RES%E2   = LHS * RHS%E2

    RES%E11  = LHS * RHS%E11
    RES%E12  = LHS * RHS%E12
    RES%E22  = LHS * RHS%E22

  END FUNCTION REAL_MUL_ONUMM2N2











  ! INTEGER POWER OF AN OTI NUMBER
  FUNCTION ONUMM2N2_IPOW(NUM, EXP) &
    RESULT(RES)
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(IN) :: NUM
    TYPE(INTEGER), INTENT(IN)   :: EXP
    TYPE(ONUM_M2N2) :: RES
    TYPE(INTEGER) :: I
    ! MULTIPLY ALL IMAGINARY DIRECTIONS.
    
    RES = NUM

    DO I = 2, EXP

      RES = RES * NUM

    END DO

  END FUNCTION ONUMM2N2_IPOW



  ! GENERAL POWER OF AN OTI NUMBER
  FUNCTION ONUMM2N2_POW(NUM, EXP) &
    RESULT(RES)
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(IN) :: NUM
    REAL(REAL_KIND), INTENT(IN) :: EXP
    REAL(REAL_KIND)             :: DERIVS(TORDER+1)
    TYPE(ONUM_M2N2) :: RES
    TYPE(INTEGER) :: I
    REAL(REAL_KIND) :: FACTOR
    REAL(REAL_KIND) :: X
    REAL(REAL_KIND) :: EXPI

    FACTOR    = 1.0D0

    X         = NUM%R
    EXPI      = EXP

    DERIVS(1) = X**EXPI
    FACTOR = EXPI

    DO I = 1, TORDER

      DERIVS(I+1) = FACTOR * X**( EXPI - I )
      FACTOR = FACTOR*( EXPI - I )

    END DO    

    RES = ONUMM2N2_FEVAL(DERIVS,NUM)

  END FUNCTION ONUMM2N2_POW







  ! SINE OF AN OTI NUMBER
  FUNCTION ONUMM2N2_SIN(NUM) &
    RESULT(RES)
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(IN) :: NUM
    REAL(REAL_KIND)             :: DERIVS(TORDER+1)
    TYPE(ONUM_M2N2) :: RES
    REAL(REAL_KIND) :: X
    
    X         = NUM%R
    
    DERIVS(1) =  SIN(X) ! Function.
    DERIVS(2) =  COS(X) ! First Order derivative.
    DERIVS(3) = -SIN(X) ! Second Order derivative.

    RES = ONUMM2N2_FEVAL(DERIVS,NUM)

  END FUNCTION ONUMM2N2_SIN

  ! COSINE OF AN OTI NUMBER
  FUNCTION ONUMM2N2_COS(NUM) &
    RESULT(RES)
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(IN) :: NUM
    REAL(REAL_KIND)             :: DERIVS(TORDER+1)
    TYPE(ONUM_M2N2) :: RES
    REAL(REAL_KIND) :: X
    
    X         = NUM%R
    
    DERIVS(1) =  COS(X) ! Function.
    DERIVS(2) = -SIN(X) ! First Order derivative.
    DERIVS(3) = -COS(X) ! Second Order derivative.

    RES = ONUMM2N2_FEVAL(DERIVS,NUM)

  END FUNCTION ONUMM2N2_COS






  ! FUNCTION EVALUATION OF AN OTI NUMBER USING TRUNCATED TAYLOR SERIES.
  FUNCTION ONUMM2N2_FEVAL(DERIVS, X) &
    RESULT(RES)
    IMPLICIT NONE
    REAL(REAL_KIND), INTENT(IN) :: DERIVS(:)
    TYPE(ONUM_M2N2), INTENT(IN) :: X
    TYPE(ONUM_M2N2) :: RES
    TYPE(ONUM_M2N2) :: DELTA
    TYPE(INTEGER)   :: I
    REAL(REAL_KIND) :: FACTOR! = 1.0D0
    
    FACTOR    = 1.0D0

    ! MULTIPLY ALL IMAGINARY DIRECTIONS.
    DELTA = X

    ! DELTA HAS REAL DIRECTION EQUAL TO 0.
    DELTA%R = 0 ! DELTA = X - X%R

    RES = DERIVS(1) ! REAL COMPONENT.

    DO I = 1, TORDER
      
      FACTOR = FACTOR*I
      RES   = RES + (DERIVS(I+1)/FACTOR)*DELTA**(I)

    END DO
  
  END FUNCTION ONUMM2N2_FEVAL


  ! DIVISION OF TWO OTI NUMBERS
  ELEMENTAL FUNCTION ONUMM2N2_DIV_REAL(LHS, RHS) &
    RESULT(RES)
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(IN) :: LHS
    REAL(REAL_KIND), INTENT(IN) :: RHS
    TYPE(ONUM_M2N2) :: RES

    ! DIVIDE ALL IMAGINARY DIRECTIONS.

    RES%R    = LHS%R /RHS

    RES%E1   = LHS%E1/RHS
    RES%E2   = LHS%E2/RHS

    RES%E11  = LHS%E11/RHS
    RES%E12  = LHS%E12/RHS
    RES%E22  = LHS%E22/RHS

  END FUNCTION ONUMM2N2_DIV_REAL

  ! DIVISION OF TWO OTI NUMBERS
  FUNCTION ONUMM2N2_DIV_ONUMM2N2(LHS, RHS) &
    RESULT(RES)
    IMPLICIT NONE
    TYPE(ONUM_M2N2), INTENT(IN) :: LHS
    TYPE(ONUM_M2N2), INTENT(IN) :: RHS
    TYPE(ONUM_M2N2) :: RES

    ! DIVIDE ALL IMAGINARY DIRECTIONS.

    RES = LHS * ( RHS**(-1.0d0) )

  END FUNCTION ONUMM2N2_DIV_ONUMM2N2

  ! DIVISION OF TWO OTI NUMBERS
  FUNCTION REAL_DIV_ONUMM2N2(LHS, RHS) &
    RESULT(RES)
    IMPLICIT NONE
    REAL(REAL_KIND), INTENT(IN) :: LHS
    TYPE(ONUM_M2N2), INTENT(IN) :: RHS
    TYPE(ONUM_M2N2) :: RES

    ! DIVIDE ALL IMAGINARY DIRECTIONS.

    RES = LHS * ( RHS**(-1.0d0) )

  END FUNCTION REAL_DIV_ONUMM2N2







  




END MODULE OTIM2N2